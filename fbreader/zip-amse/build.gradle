import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.library'

dependencies {
	api project(':api')
}

android {
	compileSdkVersion project.ext.globalCompileSdkVersion
	buildToolsVersion project.ext.globalBuildToolsVersion
	lintOptions {
		abortOnError false
	}

	sourceSets {
		main.java.srcDirs = ['src/main/java']
		main.jniLibs.srcDirs = ['src/main/libs']
		main.jni.srcDirs = []
	}

	defaultConfig {
		minSdkVersion project.ext.globalMinSdkVersion
		targetSdkVersion project.ext.globalTargetSdkVersion
		versionCode 1
		versionName "1.0"
	}

	task ndkBuild(type: Exec) {
		def ndkDir = project.android.ndkDirectory.absolutePath
		def ndkBuild = Os.isFamily(Os.FAMILY_WINDOWS) ? 'ndk-build.cmd' : 'ndk-build'
		commandLine "$ndkDir/$ndkBuild", '-C', file('src/main/jni').absolutePath
	}

	tasks.withType(JavaCompile) {
		compileTask -> compileTask.dependsOn ndkBuild
	}

	clean {
		delete 'src/main/obj'
		delete 'src/main/libs'
	}
}
